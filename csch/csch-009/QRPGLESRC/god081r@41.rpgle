     H*-------------------------------------------------------------------------
     H*■解決
     H*　例外が発生する可能性のある箇所でエラーをモニターする
     H*-------------------------------------------------------------------------
     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #08 モダンなエラーハンドリング           *
     H*   プログラムＩＤ  : GOD081R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/04                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ
     FMSYOL01@4 UF A E           K DISK
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#ERID                 40     46A                                        エラーＩＤ
     D S#PLIB                 81     90A                                        プログラム
     D S#ERMS                 91    170A                                        エラーＭＳＧ
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*----<<メインプロシージャー入口>>-----*
     D                 PI
     D  IN_001                        1A                                        引数１
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D  MSYOL01_KEY1   DS                  LIKEREC(MSYOR  : *KEY  ) INZ
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D*
     D W#MSG1          S             48A   INZ                                  メッセージ
     D E#COUNT         S              2S 0 INZ                                  エラー回数
     D E#RTN           S              6A   INZ                                  エラー制御
     C*-********************************************************************
     C*-* ＰＬＩＳＴ                                                    **
     C*-********************************************************************
     C**     *ENTRY        PLIST
     C**                   PARM                    IN_001            1            引数１
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   CALLP     #OVERFLOW()
     C*
     C                   CALLP     #DUPKEY()
     C*
     C                   CALLP     #PARAMS()
     C*
     C                   CALLP     #ARRAY()
     C*
     C                   CALLP     #OTHER()
     C*
     C***                   CALLP     #SUBR1()
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* *PSSR       ＲＰＧ例外ハンドル                           **
     C*-***************************************************************
     C     *PSSR         BEGSR
     C*
     C                   EVAL      E#COUNT    += 1
     C*
     C                   EVAL      W#MSG1      = '*PSSR-1 : '
     C                                         + %CHAR(E#COUNT )
     C                                         + '回目'
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      E#RTN       = '*CANCL'                       エラー制御
     C                   EVAL      E#RTN       = '*GETIN'                       エラー制御
     C                   EVAL      E#RTN       = '*DETC'                        エラー制御
     C                   EVAL      E#RTN       = *BLANK                         エラー制御
     C*
     C*  最大エラー回数が２回になったら終了
     C                   IF        E#COUNT    >= 2
     C                   EVAL      E#RTN       = '*CANCL'                       エラー制御
     C*  配列指標エラー
     C                   ELSEIF    S#ERID      = 'MCH0603'
     C                   EVAL      E#RTN       = '*GETIN'                       エラー制御
     C                   ELSEIF    S#ERID     <> *BLANK
     C                   EVAL      W#MSG1      = '*PSSR-2 : '
     C                                         + S#ERID
     C                                         + S#ERMS
     C     W#MSG1        DSPLY
     C                   EVAL      E#RTN       = *BLANK                         エラー制御
     C                   ENDIF
     C*
     C                   ENDSR     E#RTN
     P****************************************************************
     P** < #OVERFLOW()  > : オーバーフロー                        **
     P****************************************************************
     P #OVERFLOW       B
     D #OVERFLOW       PI
     D*---------------
     D* 変数等定義
     D*---------------
     D WA#DEC1         S              5P 0                                      数値
     D WA#DEC2         S              5P 0                                      数値
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*オーバーフローを監視する
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#OVERFLOW-1'
     C     W#MSG1        DSPLY
     C*
     C*  ＡＤＤは桁あふれしない
     C                   Z-ADD     99999         WA#DEC1                        数値
     C                   ADD       1             WA#DEC1                        数値
     C*
     C*  ＭＯＮＩＴＯＲ～ＥＮＤＭＯＮ…監視処理の開始
     C*
     C                   MONITOR
     C*
     C*  ＥＶＡＬは桁あふれする
     C                   EVAL      WA#DEC1     = 99999                          数値
     C                   EVAL      WA#DEC1    += 1                              数値
     C*
     C*  ■状況コードに応じて監視ができる
     C*    １０３…中間結果が結果を入れるだけ大きくない
     C                   ON-ERROR  103
     C                   EVAL      W#MSG1      = '103:桁あふれエラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*---------------------------------------------
     C*ゼロ割り算を監視する
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#OVERFLOW-2'
     C     W#MSG1        DSPLY
     C*
     C                   MONITOR
     C*
     C                   EVAL      WA#DEC2     = *ZERO                          数値
     C                   EVAL      WA#DEC1    /= WA#DEC2                        数値
     C*
     C*    １０２…ゼロによる除算
     C                   ON-ERROR  102
     C                   EVAL      W#MSG1      = '102:ゼロ割り算エラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C                   EVAL      W#MSG1      = '#OVERFLOW-END'
     C     W#MSG1        DSPLY
     C*
     C*-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END
     P                 E
     P****************************************************************
     P** < #DUPKEY      > : 重複キー                              **
     P****************************************************************
     P #DUPKEY         B
     D #DUPKEY         PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*重複キーを監視する
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#DUPKEY-1'
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      MSYOL01_KEY1.MSYOSYCODE = *ALL'9'            商品コード
     C     MSYOL01_KEY1  CHAIN     MSYOR
     C                   IF        %FOUND
     C                   DELETE    MSYOR
     C                   ENDIF
     C*
     C                   CLEAR                   MSYOR
     C                   EVAL      MSYOSYCODE  = *ALL'9'                        商品コード
     C                   WRITE     MSYOR
     C*
     C                   MONITOR
     C                   WRITE     MSYOR
     C*    １０２１…重複レコードの書き出し
     C                   ON-ERROR  1021
     C                   EVAL      W#MSG1      = '1021:重複キーエラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*---------------------------------------------
     C*Ｅ拡張を使用したエラーキャッチ
     C*％ＥＲＲＯＲ…たエラーキャッチ
     C*---------------------------------------------
     C                   WRITE(E)  MSYOR
     C                   IF        %ERROR
     C                   EVAL      W#MSG1      = '%ERROR書出エラー'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C                   EVAL      W#MSG1      = '#DUPKEY-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #PARAMS      > : 引数                                  **
     P****************************************************************
     P #PARAMS         B
     D #PARAMS         PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*引数エラーを監視する
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#PARAMS-1'
     C     W#MSG1        DSPLY
     C*
     C                   MONITOR
     C                   IF        IN_001      = *BLANK
     C                   ENDIF
     C*    ２２２…ポインターまたはパラメータエラー
     C                   ON-ERROR  222
     C                   EVAL      W#MSG1      = '222:引数エラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C                   EVAL      W#MSG1      = '#PARAMS-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #ARRAY       > : 配列                                  **
     P****************************************************************
     P #ARRAY          B
     D #ARRAY          PI
     D*---------------
     D* 変数等定義
     D*---------------
     D WD#ARY1         S              1A   DIM(1)                               配列
     D IX              S              2S 0 INZ                                  指標
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*指標エラーを監視する
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#ARRAY-1'
     C     W#MSG1        DSPLY
     C*
     C                   MONITOR
     C*
     C                   EVAL      IX          = *ZERO
     C                   IF        WD#ARY1(IX) = *BLANK
     C                   ENDIF
     C*    １２１…配列の指標エラー
     C                   ON-ERROR  121
     C                   EVAL      W#MSG1      = '121:配列指標エラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C                   EVAL      W#MSG1      = '#ARRAY-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #OTHER       > : その他                                **
     P****************************************************************
     P #OTHER          B
     D #OTHER          PI
     D*---------------
     D* 変数等定義
     D*---------------
     D WE#DEC1         S              5P 0                                      数値
     D WE#DEC2         S              5P 0                                      数値
     D*
     D WE#STS1         S               N                                        状況*ON=例外
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*状況コードの指定方法
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#OTHER-1'
     C     W#MSG1        DSPLY
     C*
     C*//////////////////
     C*    複数を指定
     C*//////////////////
     C                   MONITOR
     C                   EVAL      WE#DEC1     = 99999                          数値
     C                   EVAL      WE#DEC1    += 1                              数値
     C                   ON-ERROR  103 : 105
     C                   EVAL      W#MSG1      = 'プログラムエラー+
     C                                            状況コード…103,105'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*//////////////////
     C*    予約語で指定
     C*//////////////////
     C                   MONITOR
     C                   EVAL      WE#DEC1     = 99999                          数値
     C                   EVAL      WE#DEC1    += 1                              数値
     C*
     C*    ＊ＰＲＯＧＲＡＭ…１００～９９９の範囲
     C*    ％ＳＴＡＴＵＳ……状況コードを取得
     C                   ON-ERROR  *PROGRAM
     C                   EVAL      W#MSG1      = 'プログラムエラー+
     C                                            状況コード…'
     C                                         + %CHAR(%STATUS)
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*//////////////////
     C                   MONITOR
     C*
     C                   EVAL      MSYOSYCODE  = *ALL'9'                        商品コード
     C                   WRITE     MSYOR
     C                   WRITE     MSYOR
     C*    ＊ＦＩＬＥ…１０００～９９９９の範囲
     C                   ON-ERROR  *FILE
     C                   EVAL      W#MSG1      = 'ファイルエラー+
     C                                            状況コード…'
     C                                         + %CHAR(%STATUS)
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*//////////////////
     C                   MONITOR
     C*
     C                   EVAL      WE#DEC1     = 99999                          数値
     C                   EVAL      WE#DEC1    += 1                              数値
     C*    ＊ＡＬＬ…１００～９９９９の範囲
     C                   ON-ERROR  *ALL
     C                   EVAL      W#MSG1      = 'エラー+
     C                                            状況コード…'
     C                                         + %CHAR(%STATUS)
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C*//////////////////
     C*    顛末処理
     C*//////////////////
     C*    ＯＮ－ＥＸＩＴ…サブプロシージャで必ず実行される処理
     C*       https://qiita.com/ushiday/items/7c4c63625283c77e828c
     C*    ※Ｖ７Ｒ４以上（ＰＴＦ必要
     C                   EVAL      WE#DEC1     = 99999                          数値
     C                   EVAL      WE#DEC1    += 1                              数値
     C*
     C                   ON-EXIT   WE#STS1
     C*
     C                   EVAL      W#MSG1      = '#OTHER-ON-EXTI'
     C*
     C*    状況標識＝＊ＯＮは「予期しない例外（監視してない）」で終了した場合
     C                   IF        WE#STS1
     C                   EVAL      W#MSG1      = %TRIM(W#MSG1  )
     C                                         + '異常終了した'
     C                   ELSE
     C                   EVAL      W#MSG1      = %TRIM(W#MSG1  )
     C                                         + '正常終了した'
     C                   ENDIF
     C     W#MSG1        DSPLY
     C*
     C*    ＳＮＤ－ＭＳＧ…ジョブログをＲＰＧで出力
     C*       https://qiita.com/ushiday/items/1c26e003547a63346b17
     C*    ％ＴＡＲＧＥＴ…どこのレベルへメッセージ通知するか？ＳＥＬＦ＝自分自身
     C*    ※Ｖ７Ｒ５以上（ＰＴＦ必要
     C                   SND-MSG   *COMP
     C                              W#MSG1
     C                             %TARGET(*SELF  )
     C*
     C                   EVAL      W#MSG1      = '#OTHER-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #SUBR1       > : ○                                    **
     P****************************************************************
     P #SUBR1          B
     D #SUBR1          PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*あ
     C*---------------------------------------------
     C                   EVAL      W#MSG1      = '#SUBR1-1'
     C     W#MSG1        DSPLY
     C*
     C                   MONITOR
     C*    ９９９…エラー
     C                   ON-ERROR  *ALL
     C                   EVAL      W#MSG1      = '999:エラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C                   EVAL      W#MSG1      = '#OVERFLOW-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
