     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #03 疑似２次元配列（固定                 *
     H*   プログラムＩＤ  : GOD032R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/05                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*売上データ
     FSUR1P     IF   E           K DISK
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D  WTIMESTAMP     S             26Z                                        タイムスタンプ
     D  WDATE8         S              8S 0                                      日付
     D  WTIME6         S              6S 0                                      時刻
     D*
     D W#MSG1          S             48A   INZ                                  メッセージ
     D*-------------------------------------------------------------------------
     D*■修飾を利用して擬似的二次元配列にすることができる
     D*  ○○ＴＯＰ３０の様なマトリックス表などの作成
     D*-------------------------------------------------------------------------
     D*５×５（マトリックス）
     D ROWS            DS                  INZ QUALIFIED DIM(05)                行
     D  COLS                          7S 0 DIM(%ELEM(ROWS  ))                   列
     D*
     D YY              S              2S 0 INZ(1)                               縦軸（Ｙ
     D XX              S              2S 0 INZ(1)                               横軸（Ｘ
     D*
     D W#END           S               N   INZ(*OFF)                            終了フラグ
     C*-********************************************************************
     C*-* ＫＬＩＳＴ                                                    **
     C*-********************************************************************
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   DOU       %EOF(SUR1P      )
     C                   READ      SUR1R
     C                   IF        %EOF
     C                   LEAVE
     C                   ENDIF
     C*
     C                   EXSR      @SET
     C*
     C*表が満たされたら終了
     C                   IF        W#END
     C                   LEAVE
     C                   ENDIF
     C*
     C                   ENDDO
     C*
     C*表の出力
     C                   EXSR      @OUT
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   EVAL      WTIMESTAMP =  %TIMESTAMP()
     C                   EVAL      WDATE8     =  %DEC(%DATE(WTIMESTAMP) : *ISO )システム日付
     C                   EVAL      WTIME6     =  %DEC(%TIME(WTIMESTAMP) : *HMS )システム時刻
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @SET        セット処理                                   **
     C*-***************************************************************
     C     @SET          BEGSR
     C*
     C                   EVAL      ROWS(YY).COLS(XX) = S1SU01
     C*
     C                   EVAL      YY         += 1
     C*
     C*  縦方向オーバーフロー
     C                   IF        YY          > %ELEM(ROWS     )
     C                   EVAL      XX         += 1
     C                   EVAL      YY          = 1
     C                   ENDIF
     C*
     C*  横方向オーバーフロー
     C                   IF        XX          > %ELEM(ROWS.COLS )
     C                   EVAL      W#END       = *ON
     C                   ENDIF
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @OUT        出力処理                                     **
     C*-***************************************************************
     C     @OUT          BEGSR
     C*
     C                   EVAL      W#MSG1      = *ALL'-'
     C                   EVAL      W#MSG1      = %SUBST(W#MSG1   :1 :41)
     C     W#MSG1        DSPLY
     C*  行方向出力
     C                   FOR       YY          = 1 TO %ELEM(ROWS      )
     C*
     C                   EVAL      W#MSG1      = '|'
     C*
     C*    列方向出力
     C                   FOR       XX          = 1 TO %ELEM(ROWS.COLS )
     C                   EVAL      W#MSG1      = %TRIMR(W#MSG1  )
     C                                         + %EDITC(ROWS(YY).COLS(XX) :'3')
     C                                         + '|'
     C                   ENDFOR
     C*
     C     W#MSG1        DSPLY
     C*
     C                   ENDFOR
     C*
     C                   EVAL      W#MSG1      = *ALL'-'
     C                   EVAL      W#MSG1      = %SUBST(W#MSG1   :1 :41)
     C     W#MSG1        DSPLY
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
