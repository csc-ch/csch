     H*-------------------------------------------------------------------------
     H*■
     H*-------------------------------------------------------------------------
     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #09 意外と使われていないリング           *
     H*   プログラムＩＤ  : GOD091R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/05                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ
     FMSYOL01@4 UF A E           K DISK
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#ERID                 40     46A                                        エラーＩＤ
     D S#PLIB                 81     90A                                        プログラム
     D S#ERMS                 91    170A                                        エラーＭＳＧ
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*----<<メインプロシージャー入口>>-----*
     D                 PI
     D  IN_CNT                        1P 0 OPTIONS(*NOPASS   )                  再帰回数
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D  MSYOL01_KEY1   DS                  LIKEREC(MSYOR  : *KEY  ) INZ
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D W#MSG1          S             48A   INZ                                  メッセージ
     D W#CNT           S              1P 0 INZ                                  再帰回数
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   CALLP     #CONST()
     C*
     C                   CALLP     #BOOL()
     C*
     C                   CALLP     #DATETIME()
     C*
     C                   CALLP     #MOVE()
     C*
     C                   CALLP     #CALL()
     C*
     C***                   CALLP     #SUBR1()
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C*再帰呼出し
     C                   EVAL      W#CNT       = *ZERO
     C                   IF        %PASSED(IN_CNT     )
     C                   EVAL      W#CNT       = IN_CNT
     C                   ENDIF
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
     P****************************************************************
     P** < #CONST       > : 定数の活用                            **
     P****************************************************************
     P #CONST          B
     D #CONST          PI
     D*---------------
     D* 変数／定数定義
     D*---------------
     D*画面位置の定義
     D W#DSP           S              4A   INZ('END')                           画面位置
     D  C#DSP_1        C                   'DSP1'                               画面１
     D  C#DSP_2        C                   'DSP2'                               画面２
     D  C#DSP_E        C                   'END'                                画面終了
     D*
     D*商品分類
     D*※ＦＦならばＥＮＵＭ／列挙定義も使える（Ｖ７Ｒ５以降）
     D W#BUN           S              2A                                        商品分類
     D W#BUN_CONST     S              2A   CONST INZ('99')                      変更不可変数
     D*
     D C#BUN_INRYO     C                   '01'                                 分類（飲料
     D C#BUN_OKASHI    C                   '02'                                 分類（お菓子
     D C#BUN_CHOMIRYO  C                   '03'                                 分類（調味料
     D C#BUN_SEISEN    C                   '04'                                 分類（生鮮食品
     D C#BUN_NICHIZAKKA...
     D                 C                   '11'                                 分類（日用雑貨
     D C#BUN_KITCHEN   C                   '12'                                 分類（キッチン用品
     D C#BUN_BUNBOGU   C                   '13'                                 分類（文房具
     D C#BUN_EISEI     C                   '21'                                 分類（衛生用品
     D C#BUN_BIYOU     C                   '22'                                 分類（美容健康品
     D C#BUN_INTERIOR  C                   '31'                                 分類（インテリア
     D*
     D C#BUN           DS                  QUALIFIED CONST                      商品分類群
     D  INRYO                              LIKE(W#BUN     ) INZ('01')           分類（飲料
     D  OKASHI                             LIKE(W#BUN     ) INZ('02')           分類（お菓子
     D  CHOMIRYO                           LIKE(W#BUN     ) INZ('03')           分類（調味料
     D  SEISEN                             LIKE(W#BUN     ) INZ('04')           分類（生鮮食品
     D  NICHIZAKKA                         LIKE(W#BUN     ) INZ('11')           分類（日用雑貨
     D  KITCHEN                            LIKE(W#BUN     ) INZ('12')           分類（キッチン用品
     D  BUNBOGU                            LIKE(W#BUN     ) INZ('13')           分類（文房具
     D  EISEI                              LIKE(W#BUN     ) INZ('21')           分類（衛生用品
     D  BIYOU                              LIKE(W#BUN     ) INZ('22')           分類（美容健康品
     D  INTERIOR                           LIKE(W#BUN     ) INZ('31')           分類（インテリア
     D*
     D*配列の定義
     D W#ARY0          S             64A   DIM(10       )                       配列
     D W#ARY1          S             64A   DIM(C#MAX1   )                       配列
     D  C#MAX1         C                   10                                   最大要素数
     D W#ARY2          S                   LIKE(W#ARY1  ) DIM(C#MAX1   )        配列
     D W#ARY3          S                   LIKE(W#ARY1  ) DIM(%ELEM(W#ARY1 ))   配列
     D IX              S              5I 0                                      指標
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）定数を使わない場合
     C*    ・リテラルを間違えやすい
     C*    ・開発者がコードを理解している必要がある
     C*    ・タイポしてもコンパイラで引っ掛けられない
     C*
     C******************************************************************
     C*１）画面処理でよく見かけるリテラル
     C******************************************************************
     C*    -----------
     C*    リテラル版
     C*    -----------
     C                   DOU       W#DSP      =  'END'
     C     W#DSP         CASEQ     'DSP1'        @DSP1                          第１画面
     C     W#DSP         CASEQ     'DSP2'        @DSP2                          第２画面
     C                   ENDCS
     C                   ENDDO
     C*
     C*    -----------
     C*    定数版
     C*    -----------
     C*    「／ＣＯＰＹ」を使い共通定義仕様とすることもできる
     C                   DOU       W#DSP      =  C#DSP_E
     C     W#DSP         CASEQ     C#DSP_1       @DSP1                          第１画面
     C     W#DSP         CASEQ     C#DSP_2       @DSP2                          第２画面
     C                   ENDCS
     C                   ENDDO
     C*
     C******************************************************************
     C*２）分類やコードの条件分岐とうのリテラル
     C******************************************************************
     C*    -----------
     C*    リテラル版
     C*    -----------
     C                   IF        W#BUN = '01' OR W#BUN = '02'
     C                   ENDIF
     C*
     C                   IF        W#BUN  <> '11' AND W#BUN  <> '12' AND
     C                             W#BUN  <> '21' AND W#BUN  <> '31'
     C                   ENDIF
     C*
     C*    -----------
     C*    定数版
     C*    -----------
     C                   EVAL      W#BUN       = C#BUN.INRYO
     C*
     C                   IF        W#BUN       = C#BUN.INRYO         OR
     C                             W#BUN       = C#BUN.OKASHI
     C                   EVAL      W#MSG1      = '飲料かお菓子です'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C                   IF        W#BUN      <> C#BUN.NICHIZAKKA    AND
     C                             W#BUN      <> C#BUN.KITCHEN       AND
     C                             W#BUN      <> C#BUN.EISEI         AND
     C                             W#BUN      <> C#BUN.BIYOU
     C                   EVAL      W#MSG1      = '日用+
     C                                           ,キッチン+
     C                                           ,衛生+
     C                                           ,美容健康品以外です'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C******************************************************************
     C*３）配列の要素数のリテラル
     C******************************************************************
     C*    -----------
     C*    リテラル版
     C*    -----------
     C     1             DO        10            IX
     C                   EVAL      W#ARY0(IX)  = 'X'
     C                   ENDDO
     C*
     C                   FOR       IX          = 1 TO 10
     C                   EVAL      W#ARY0(IX)  = 'X'
     C                   ENDFOR
     C*
     C*    -----------
     C*    定数版
     C*    -----------
     C     1             DO        C#MAX1        IX
     C                   EVAL      W#ARY1(IX)  = 'X'
     C                   ENDDO
     C*
     C                   FOR       IX          = 1 TO %ELEM(W#ARY2 )
     C                   EVAL      W#ARY2(IX)  = 'X'
     C                   ENDFOR
     C*
     C*-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END
     C     @DSP1         BEGSR
     C                   ENDSR
     C     @DSP2         BEGSR
     C                   ENDSR
     P                 E
     P****************************************************************
     P** < #BOOL        > : ブール型                              **
     P****************************************************************
     P #BOOL           B
     D #BOOL           PI
     D*---------------
     D* 変数等定義
     D*---------------
     D OT2_ERR         S               N                                        エラーフラグ
     D*
     D*商品分類
     D W#BUN           S              2A                                        商品分類
     D W#SYOKHIN       S              1N                                        食品フラグ
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）標識や”１Ａ”の代わりにブール型変数で可読性やミスを減らす
     C*    ・標識は番号なので間違えやすい間違えやすい
     C*    ・開発者が標識番号／１Ａの意味を理解している必要がある
     C*    ・番号や文字の場合タイポしてもコンパイラで引っ掛けられない
     C*
     C******************************************************************
     C*１）パラメータの戻り値をわかりやすくする
     C******************************************************************
     C*    -----------
     C*    １Ａ版
     C*    -----------
     C                   CALL      '#MOK001R'
     C                   PARM      '12345'       IN1_SYCODE        5            商品コード
     C                   PARM      123           IN1_SYTANK        5 0          販売単価
     C                   PARM      100           IN1_SURYO         7 0          数量
     C                   PARM      *ZERO         OT1_KING         11 0          金額
     C                   PARM      *ZERO         OT1_ERR           1            エラー
     C*
     C*    「ＯＴ１＿ＥＲＲ」は値が何が成功で何が失敗か分かりづらい
     C*        ”＊ＢＬＡＮＫ”が成功？
     C*        ”Ｓ”が成功？
     C*        ”１”が失敗？
     C*        ”９”が失敗？
     C*        ０（ゼロ）…成功
     C*        Ｅ……………失敗
     C                   IF        OT1_ERR     = '0'
     C                   EVAL      W#MSG1      = '１Ａ）金額…'
     C                                         + %EDITC(OT1_KING   : 'N')
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*    -----------
     C*    １Ｎ版
     C*    -----------
     C                   CALL      '#MOK002R'
     C                   PARM      'ABCDE'       IN2_SYCODE        5            商品コード
     C                   PARM      987           IN2_SYTANK        5 0          販売単価
     C                   PARM      1000          IN2_SURYO         7 0          数量
     C                   PARM      *ZERO         OT2_KING         11 0          金額
     C                   PARM      *OFF          OT2_ERR                        エラー
     C*
     C*    「ＯＴ２＿ＥＲＲ」は”エラーが＊ＯＮ／＊ＯＦＦ”なので分かりやすい
     C*        ＊ＯＦＦ…成功
     C*        ＊ＯＮ……失敗
     C                   IF        NOT OT2_ERR
     C                   EVAL      W#MSG1      = '１Ｎ）金額…'
     C                                         + %EDITC(OT2_KING   : 'N')
     C     W#MSG1        DSPLY
     C                   ENDIF
     C******************************************************************
     C*２）標識を名前付きにしてわかりやすくする
     C******************************************************************
     C*    -----------
     C*    標識版
     C*    -----------
     C*    標識の場合、ロジック判定箇所と標識利用箇所が離れると、コードの見通しが悪い
     C*    ７１番の意味を開発者が理解している必要がある
     C*    初見の場合は番号の意味の整理から始まってしまい、飲み込みに時間を要する
     C                   EVAL      W#BUN       = '01'
     C                   SETOFF                                       71
     C*
     C*   食品の判定
     C                   IF        W#BUN = '01' OR W#BUN = '02' OR
     C                             W#BUN = '03' OR W#BUN = '04'
     C                   SETON                                        71
     C                   ENDIF
     C*
     C*    -----------
     C*    １Ｎ版
     C*    -----------
     C*    ロジック判定箇所とフラグ利用箇所が離れても、名前付き意味があり、分かりやすい
     C*    初見の場合でも、変数の命名ルールなどが飲み込めてくれば、比較的理解がしやすい
     C                   EVAL      W#SYOKHIN   = *OFF
     C*
     C*   食品の判定
     C                   IF        W#BUN       = '01'                OR
     C                             W#BUN       = '02'                OR
     C                             W#BUN       = '03'                OR
     C                             W#BUN       = '04'
     C                   EVAL      W#SYOKHIN   = *ON
     C                   ENDIF
     C*
     P                 E
     P****************************************************************
     P** < #DATETIME    > : 日付時刻型                            **
     P****************************************************************
     P #DATETIME       B
     D #DATETIME       PI
     D*---------------
     D* 変数等定義
     D*---------------
     D W#TSNOW         S             26Z                                        タイムスタンプ
     D W#DTNOW         S             10D                                        日付
     D W#TMNOW         S              8T                                        時刻
     D W#TS1           S               Z                                        タイムスタンプ
     D W#DT1           S               D                                        日付
     D W#TM1           S               T                                        時刻
     D*
     D W#DATE8         S              8S 0                                      日付（８桁
     D W#TIME6         S              6S 0                                      時刻（６桁
     D*
     D W#Y4            S              4S 0                                      年
     D W#M2            S              2S 0                                      月
     D W#D2            S              2S 0                                      日
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）日付／時刻型を使い、演算を簡単に行う
     C*
     C******************************************************************
     C*１）日付時刻の操作方法
     C*　　日付時刻型と伝統的な数値型を変換で行き来できる
     C*　　数値演算だと煩雑になりがちな計算をシンプルにできる
     C******************************************************************
     C*
     C*  現在の日時
     C*    ％ＴＩＭＥＳＴＡＭＰ…タイムスタンプの処理。引数なしは現在
     C                   EVAL      W#TSNOW     = %TIMESTAMP()
     C                   EVAL      W#MSG1      = %CHAR(W#TSNOW   )
     C     W#MSG1        DSPLY
     C*
     C*  現在の日付
     C*    ％ＤＡＴＥ…日付の処理
     C                   EVAL      W#DTNOW     = %DATE(W#TSNOW   )
     C                   EVAL      W#MSG1      = %CHAR(W#DTNOW   )
     C     W#MSG1        DSPLY
     C*
     C*  現在の時刻
     C*    ％ＴＩＭＥ…時刻の処理
     C                   EVAL      W#TMNOW     = %TIME(W#TSNOW   )
     C                   EVAL      W#MSG1      = %CHAR(W#TMNOW   )
     C     W#MSG1        DSPLY
     C*
     C*  日付の加減算
     C*    ％ＹＥＡＲＳ……年単位の処理
     C*    ％ＭＯＮＴＨＳ…月単位の処理
     C*    ％ＤＡＹＳ………日単位の処理
     C                   EVAL      W#DT1       = W#DTNOW
     C                                         + %YEARS(1  )
     C                                         + %MONTHS(1  )
     C                                         + %DAYS(1  )
     C                   EVAL      W#MSG1      = '”１年１ヶ月１日”後は…'
     C                                         + %CHAR(W#DT1     )
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      W#DT1       = W#DTNOW
     C                                         - %YEARS(1  )
     C                                         - %MONTHS(1  )
     C                                         - %DAYS(1  )
     C                   EVAL      W#MSG1      = '”１年１ヶ月１日”前は…'
     C                                         + %CHAR(W#DT1     )
     C     W#MSG1        DSPLY
     C*
     C*  時刻の加減算
     C*    ％ＨＯＵＲＳ………時単位の処理
     C*    ％ＭＩＮＵＴＥＳ…分単位の処理
     C*    ％ＳＥＣＯＮＤＳ…秒単位の処理
     C                   EVAL      W#TS1       = W#TSNOW
     C                                         + %HOURS(1  )
     C                                         + %MINUTES(1  )
     C                                         + %SECONDS(1  )
     C                   EVAL      W#TM1       = %TIME(W#TS1     )
     C                   EVAL      W#MSG1      = '”１時間１分１秒”後は…'
     C                                         + %CHAR(W#TM1     )
     C     W#MSG1        DSPLY
     C*
     C*  時差を取得
     C*    ％ＤＩＦＦ…２つの時間の差を求める
     C                   EVAL      W#MSG1      = '私が生まれて'
     C                                         + %CHAR(%DIFF(
     C                                               W#TSNOW
     C                                             : %DATE(19780106)
     C                                             : *YEARS
     C                                           ))
     C                                         + '年が経ちます'
     C     W#MSG1        DSPLY
     C                   EVAL      W#MSG1      = '私が生まれて'
     C                                         + %CHAR(%DIFF(
     C                                               W#TSNOW
     C                                             : %DATE(19780106)
     C                                             : *DAYS
     C                                           ))
     C                                         + '日が経ちます'
     C     W#MSG1        DSPLY
     C*
     C*  伝統的な数値型へ変換（日付
     C                   EVAL      W#DATE8     = %DEC(W#DT1     : *ISO  )
     C                   EVAL      W#MSG1      = %CHAR(W#DATE8   )
     C     W#MSG1        DSPLY
     C*
     C*  伝統的な数値型へ変換（時刻
     C                   EVAL      W#TIME6     = %DEC(W#TM1     : *HMS  )
     C                   EVAL      W#MSG1      = %CHAR(W#TIME6   )
     C     W#MSG1        DSPLY
     C*
     C*  数値型を日付型へ変換
     C                   EVAL      W#DT1       = %DATE(W#DATE8  : *ISO  )
     C                   EVAL      W#MSG1      = %CHAR(W#DT1     )
     C     W#MSG1        DSPLY
     C*
     C*  数値型を時刻型へ変換
     C                   EVAL      W#TM1       = %TIME(W#TIME6  : *HMS  )
     C                   EVAL      W#MSG1      = %CHAR(W#TM1     )
     C     W#MSG1        DSPLY
     C*
     C******************************************************************
     C*２）タイムスタンプをデータベースに出力
     C*　　オープン系のデータベースでは日付型を使うケースが殆ど
     C******************************************************************
     C                   EVAL      MSYOL01_KEY1.MSYOSYCODE = *ALL'9'            商品コード
     C     MSYOL01_KEY1  CHAIN     MSYOR
     C                   IF        NOT %FOUND
     C                   CLEAR                   MSYOR
     C                   ENDIF
     C*
     C                   EVAL      MSYOSYCODE  = *ALL'9'                        商品コード
     C                   EVAL      MSYOTIMSTP  = W#TSNOW                        タイムスタンプ
     C*
     C                   IF        NOT %FOUND
     C                   WRITE     MSYOR
     C                   ELSE
     C                   UPDATE    MSYOR
     C                   ENDIF
     C                   EVAL      W#MSG1      = 'データベースに+
     C                                            タイムスタンプを出力'
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #MOVE        > : ＭＯＶＥｘ                            **
     P****************************************************************
     P #MOVE           B
     D #MOVE           PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）ＲＰＧⅢからコンバージョンの場合特に
     C*    （Ｐ）オプションをつかっていないケースが多い
     C*
     C******************************************************************
     C*１）ＭＯＶＥ、ＭＯＶＥＬ※オプションなし
     C******************************************************************
     C*    -----------
     C*    Ｐなし
     C*    -----------
     C                   MOVEL     *ALL'X'       W#MSG1                         メッセージ
     C*
     C*  ”ＡＢＣ”としたいため
     C*    クリアするために一旦”＊ＢＬＡＮＫ”の１ステップを要している
     C                   MOVEL     *BLANK        W#MSG1                         メッセージ
     C                   MOVEL     'ABC'         W#MSG1                         メッセージ
     C*
     C*  ”ＡＢＣ”としたいのに忘れて”ＡＢＣＸＸＸ...”になってしまっている
     C                   MOVEL     *ALL'X'       W#MSG1                         メッセージ
     C                   MOVEL     'ABC'         W#MSG1                         メッセージ
     C*
     C*    -----------
     C*    Ｐあり
     C*    -----------
     C*  （Ｐ）オプションやＥＶＡＬを使うとシンプルとなり、消し忘れもない
     C                   MOVEL(P)  *ALL'X'       W#MSG1                         メッセージ
     C                   MOVEL(P)  'ABC'         W#MSG1                         メッセージ
     C     W#MSG1        DSPLY
     C*
     C*    -----------
     C*    ＥＶＡＬ
     C*    -----------
     C                   EVAL      W#MSG1      = *ALL'X'                        メッセージ
     C                   EVAL      W#MSG1      = 'ABC'                          メッセージ
     C     W#MSG1        DSPLY
     C*
     P                 E
     P****************************************************************
     P** < #CALL        > : 再帰呼出し                            **
     P****************************************************************
     P #CALL           B
     D #CALL           PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）ネストした処理を再帰呼出しで行える
     C*    コンパイルオプション「DFTACTGRP(*NO) ACTGRP(*NEW)」が必要
     C*    ↑↑↑Ｈ仕様書記述で毎回手動行う必要がない
     C*      -----------------------------
     C*     H DFTACTGRP(*NO) ACTGRP(*NEW)
     C*      -----------------------------
     C******************************************************************
     C*１）再帰呼出し
     C******************************************************************
     C*  サンプルプログラムは最大３回まで再帰する
     C                   EVAL      W#CNT      += 1                              再帰回数
     C                   IF        W#CNT       > 3
     C                   RETURN
     C                   ENDIF
     C*
     C                   EVAL      W#MSG1      = '再帰回数…'                 メッセージ
     C                                         + %CHAR(W#CNT  )
     C                                         + '回目（※最大３回まで）'
     C     W#MSG1        DSPLY
     C*
     C                   CALL      S#PROC
     C                   PARM      W#CNT         IN4_CNT           1 0          再帰回数
     C*
     P                 E
     P****************************************************************
     P** < #SUBR1       > : ○                                    **
     P****************************************************************
     P #SUBR1          B
     D #SUBR1          PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*例）定数を使わない場合
     C*    ・リテラルを間違えやすい
     C*    ・開発者がコードを理解している必要がある
     C*    ・タイポしてもコンパイラで引っ掛けられない
     C*
     C******************************************************************
     C*１）分類やコードの条件分岐とうのリテラル
     C******************************************************************
     C*    -----------
     C*    リテラル版
     C*    -----------
     C*
     C*    -----------
     C*    定数版
     C*    -----------
     C*    「／ＣＯＰＹ」を使い共通定義仕様とすることもできる
     C                   EVAL      W#MSG1      = '#SUBR1-1'
     C     W#MSG1        DSPLY
     C*
     C                   MONITOR
     C*    ９９９…エラー
     C                   ON-ERROR  *ALL
     C                   EVAL      W#MSG1      = '999:エラー'
     C     W#MSG1        DSPLY
     C*
     C                   ENDMON
     C*
     C                   EVAL      W#MSG1      = '#OVERFLOW-END'
     C     W#MSG1        DSPLY
     C*
     P                 E
