     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #05 便利な…EXTDESC（固定              *
     H*   プログラムＩＤ  : GOD051R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/08/27                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ（コード順／読取用
     FMSYOL01@4 IF   E           K DISK    RENAME(MSYOR      :MSYOR01    )
     F*■効率的／効果的な点
     F*　ＥＸＴＤＥＳＣ…コンパイル時に使われるファイル
     F*　ＥＸＴＦＩＬＥ…実行時に使われるファイル
     F*　　　　　　　　（※ＱＴＥＭＰを明示することもできる）
     F*　
     F*　２つを組み合わせて使うことで...
     F*　　不要なＤＤＳやＯＶＲＤＢＦをせず、同一のアクセスパスをＲＰＧで扱える
     F*　　ＲＰＧ内で全て完結しているため見通しが良い
     F*　
     F*  ※注意…ＲＥＮＡＭＥより前に宣言する必要がある
     F*商品マスタ（コード順／更新用
     FXSYOL01@4 UF   E           K DISK    EXTDESC('MSYOL01@4')
     F                                     EXTFILE(*EXTDESC)
     F                                     RENAME(MSYOR      :XSYOR01    )
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D XSYOL01_KEY1    DS                  LIKEREC(XSYOR01    : *KEY    ) INZ
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D WTIMESTAMP      S             26Z                                        タイムスタンプ
     D WDATE8          S              8S 0                                      日付
     D WTIME6          S              6S 0                                      時刻
     D*
     D W#MSG1          S             48A   INZ VARYING                          メッセージ
     D*
     D IX              S              3I 0 INZ                                  ＩＮＤＥＸ
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   EVAL      XSYOL01_KEY1.MSYOSYCODE                      商品コード
     C                                         = *HIVAL
     C     XSYOL01_KEY1  SETGT     XSYOR01
     C*
     C                   FOR       IX          = 1 TO 3
     C*
     C                   READ      MSYOR01
     C                   IF        %EOF
     C                   LEAVE
     C                   ENDIF
     C*
     C                   EVAL      W#MSG1      = 'R:'
     C                                         + MSYOSYCODE
     C                                         + ' '
     C                                         + MSYOSYNAME
     C     W#MSG1        DSPLY
     C*
     C                   READP     XSYOR01
     C                   IF        %EOF
     C                   ITER
     C                   ENDIF
     C*
     C                   EVAL      MSYOUPDDAT  = WDATE8                         更新日付
     C                   EVAL      MSYOUPDTIM  = WTIME6                         更新時刻
     C                   EVAL      MSYOUPDPGM  = S#PROC                         更新ＰＧＭ
     C                   UPDATE    XSYOR01
     C*
     C                   EVAL      W#MSG1      = 'U:'
     C                                         + MSYOSYCODE
     C                                         + ' '
     C                                         + MSYOSYNAME
     C     W#MSG1        DSPLY
     C*
     C                   ENDFOR
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   EVAL      WTIMESTAMP =  %TIMESTAMP()
     C                   EVAL      WDATE8     =  %DEC(%DATE(WTIMESTAMP) : *ISO )システム日付
     C                   EVAL      WTIME6     =  %DEC(%TIME(WTIMESTAMP) : *HMS )システム時刻
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
