     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #01 ６桁命名の呪縛（固定                 *
     H*   プログラムＩＤ  : GOD011R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/07/10                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ
     F* - ALIAS :エイリアス・フィールド側を使う
     FMSYOP@4   IF   E           K DISK    ALIAS
     D*-*******************************************************************
     D*-*Ｄ仕様書                                                      **
     D*-*******************************************************************
     D W#MSG1          S             48A   INZ VARYING                          メッセージ
     D*
     D IX              S              3I 0 INZ                                  ＩＮＤＥＸ
     D*
     D W#LONGLONG_VAR_NAME...
     D                 S             48A   INZ                                  長い変数名
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   FOR       IX          = 1 TO 3
     C                   READ      MSYOR
     C                   IF        %EOF
     C                   LEAVE
     C                   ENDIF
     C*
     C*長い変数名
     C                   EVAL      W#LONGLONG_VAR_NAME
     C                                         = MSYOP_SYOHIN_CODE
     C                                         + ' '
     C                                         + MSYOP_SYOHIN_NAME
     C*
     C*１０桁フィールド
     C***                   EVAL      W#MSG1      = MSYOSYCODE
     C***                                         + ' '
     C***                                         + MSYOSYNAME
     C*
     C*エイリアス・フィールド
     C                   EVAL      W#MSG1      = MSYOP_SYOHIN_CODE
     C                                         + ' '
     C                                         + MSYOP_SYOHIN_NAME
     C*
     C     W#MSG1        DSPLY
     C*
     C                   ENDFOR
     C*
     C                   DUMP(A)
     C*
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
