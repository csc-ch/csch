     H*-------------------------------------------------------------------------
     H*■解決
     H*　便利なＢＩＦ（組込関数）を利用し効率的なコーディングをする
     H*-------------------------------------------------------------------------
     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #07 便利なＢＩＦ／組込関数               *
     H*   プログラムＩＤ  : GOD071R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/03                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D*
     D W#MSG1          S             48A   INZ                                  メッセージ
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   CALLP     #STRING1()
     C*
     C                   CALLP     #LENGTH1()
     C*
     C                   CALLP     #EOF1()
     C*
     C                   CALLP     #SUBR1()
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
     P****************************************************************
     P** < #STRING1     > : 文字列操作                            **
     P****************************************************************
     P #STRING1        B
     D #STRING1        PI
     D*---------------
     D* 変数等定義
     D*---------------
     D WA#DEC1         S              5P 0                                      数値
     D WA#DEC2         S              7P 2                                      数値
     D*
     D WA#STR1         S             64A                                        文字列
     D WA#STR2         S             64A   VARYING                              文字列
     D*
     D CA#ERR1         C                   'ERR1010001予期しないエラー発生'   エラー
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*数値を文字列表現するための「文字化」は不要
     C*---------------------------------------------
     C*  ％ＣＨＡＲ…数値を文字列へ変換できる（カンマなし、ゼロサプレス状態）
     C                   Z-ADD     1234          WA#DEC1                        数値
     C                   EVAL      W#MSG1      = %CHAR(WA#DEC1    )
     C     W#MSG1        DSPLY
     C*
     C*  数値式なども変換できる
     C                   EVAL      W#MSG1      = %CHAR( WA#DEC1 /-100 )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＤＥＣＨ…指定した桁数と小数部へパック変換※四捨五入
     C                   EVAL      W#MSG1      = %CHAR(
     C                                            %DECH( WA#DEC1 /-100 :5 :2)
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＥＤＩＴＣ…帳票や画面で使用できる”ＥＤＴＣＤＥ”と類似した関数
     C                   Z-ADD     12345.67      WA#DEC2                        数値
     C                   EVAL      W#MSG1      = %EDITC( WA#DEC2 : 'N'  )
     C     W#MSG1        DSPLY
     C*
     C*   「H DECEDIT('0.')」制御で”＿．１３”以外にも”０．１３”が表現できる
     C                   EVAL      W#MSG1      = %EDITC(
     C                                              %DECH( 0.125  :3 :2)
     C                                            : 'N'  )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＥＤＩＴＷ…帳票や画面で使用できる”ＥＤＴＷＲＤ”と類似した関数
     C                   EVAL      W#MSG1      = %EDITW(
     C                                                 20250901
     C                                               : '    /  /  '
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      W#MSG1      = %EDITW(
     C                                                 WDATE8
     C                                            : '    年  月  日'
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*---------------------------------------------
     C*不要なブランクの除去はとても簡単
     C*---------------------------------------------
     C                   EVAL      WA#STR1     = '     あいうえお'
     C                   EVAL      WA#STR2     = 'ＡＢＣＤＥＦ   '
     C*
     C*  ”ＳＴＲ１”は固定長のため切れてしまう
     C                   EVAL      W#MSG1      = WA#STR1  + WA#STR2
     C     W#MSG1        DSPLY
     C*
     C*  ％ＴＲＩＭＲ…後方ブランクを除去すれば連結文字列表現ができる
     C                   EVAL      W#MSG1      = %TRIMR( WA#STR1  )
     C                                         + WA#STR2
     C     W#MSG1        DSPLY
     C*
     C*  ％ＴＲＩＭＬ…前方ブランクを除去する
     C                   EVAL      W#MSG1      = %TRIML(%TRIMR( WA#STR1  ))
     C                                         + WA#STR2
     C     W#MSG1        DSPLY
     C*
     C*  ％ＴＲＩＭ……前後方ブランクを除去する
     C                   EVAL      W#MSG1      = %TRIM( WA#STR1   )
     C                                         + WA#STR2
     C     W#MSG1        DSPLY
     C*
     C*  ＶＡＲＹＩＮＧ…可変長文字列では意図的に埋めたブランク以外は意識しなくて良い
     C                   EVAL      W#MSG1      = WA#STR2
     C                                         + %TRIM(WA#STR1   )
     C     W#MSG1        DSPLY
     C*
     C*---------------------------------------------
     C*文字列の部分取得／部分変更が可能
     C*---------------------------------------------
     C*  ％ＳＵＢＳＴ…文字列の部分取得。式の中で展開が可能
     C*  ・受皿となる不要な変数などが減り、可読性が高まる
     C                   EVAL      W#MSG1      = 'ERR ID:'
     C                                         + %SUBST(CA#ERR1  :4  :7  )
     C                                         + '('
     C                                         + %SUBST(CA#ERR1  :11 )
     C                                         + ')'
     C     W#MSG1        DSPLY
     C*
     C*  評価の左辺として文字列の一部を置換することもできる
     C*  ・”ＥＲＲ”→”ＷＡＲ”に置換
     C                   EVAL      WA#STR1     = CA#ERR1
     C                   EVAL      %SUBST(WA#STR1  :1  :3)  = 'WAR'
     C                   EVAL      W#MSG1      = WA#STR1
     C     W#MSG1        DSPLY
     C*
     C*  ％ＲＥＰＬＡＣＥ…文字列の一部を置換
     C*  ・”ＷＡＲ”→”ＩＮＦ”に置換
     C                   EVAL      WA#STR1     = %REPLACE('INF' : WA#STR1  )
     C                   EVAL      W#MSG1      = WA#STR1
     C     W#MSG1        DSPLY
     C*
     C*---------------------------------------------
     C*特定文字を指定文字で置換
     C*---------------------------------------------
     C*  ％ＳＣＡＮＲＰＬ…特定文字を指定の文字で置換
     C*  ・電話番号の”ハイフン”を”空文字”に置換して数値だけを出力
     C*  （郵便番号の”ハイフン、日付の”スラッシュ”などに使える）
     C                   EVAL      WA#STR1     = '054-268-1618'
     C                   EVAL      W#MSG1      = %SCANRPL('-' : '' : WA#STR1  )
     C     W#MSG1        DSPLY
     C*
     C*-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END-END
     P                 E
     P****************************************************************
     P** < #LENGTH1     > : 長さ／サイズ                          **
     P****************************************************************
     P #LENGTH1        B
     D #LENGTH1        PI
     D*---------------
     D* 変数等定義
     D*---------------
     D WB#STR1         S             48A                                        文字列
     D WB#STR2         S             48A   VARYING                              文字列
     D WB#STR3         S             24G                                        図形
     D*
     D CB#MAX1         C                   24                                   配列最大サイズ
     D WB#ARY1         S              2A   DIM(CB#MAX1   )                      配列
     D WB#ARY2         S                   LIKE(WB#ARY1        )                配列
     D                                     DIM(%ELEM(WB#ARY1  ))
     D                                     INZ('--')
     D IX              S              2S 0 INZ                                  配列指標
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*---------------------------------------------
     C*有効桁数などの取得はとても簡単
     C*---------------------------------------------
     C                   EVAL      WB#STR1     = 'ｱｲｳ'
     C                   EVAL      WB#STR2     = 'ABCDE'
     C                   EVAL      WB#STR3     = G'あい'
     C*
     C*  ％ＬＥＮ…長さを取得
     C                   EVAL      W#MSG1      = '残り'
     C                                         + %CHAR(
     C                                              %LEN(WB#STR1 )
     C                                              - %LEN(%TRIMR(WB#STR1 ))
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*  可変長は”＊ＭＡＸ”で最大サイズを取得
     C                   EVAL      W#MSG1      = '残り'
     C                                         + %CHAR(
     C                                              %LEN(WB#STR2 : *MAX)
     C                                              - %LEN(WB#STR2  )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＳＩＺＥ…実バイト数を取得
     C                   EVAL      W#MSG1      = 'サイズ固定'
     C                                         + %CHAR(
     C                                              %SIZE(WB#STR1 )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      W#MSG1      = 'サイズ可変'
     C                                         + %CHAR(
     C                                              %SIZE(WB#STR2 )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      W#MSG1      = '長さＧ'
     C                                         + %CHAR(
     C                                              %LEN(WB#STR3 )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C                   EVAL      W#MSG1      = 'サイズＧ'
     C                                         + %CHAR(
     C                                              %SIZE(WB#STR3 )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＥＬＥＭ…配列の要素数を取得
     C*  ・別の配列定義の要素数設定にも使用可能
     C*    ”ＤＩＭ（％ＥＬＥＭ（配列名））”
     C                   FOR       IX          = 1 TO %ELEM(WB#ARY1  )
     C                   EVAL      WB#ARY1(IX) = %EDITC(IX : 'X')
     C                   ENDFOR
     C*
     C                   EVAL      W#MSG1      = '配列要素数:'
     C                                         + %CHAR(
     C                                              %ELEM(WB#ARY1  )
     C                                           )
     C     W#MSG1        DSPLY
     C*
     C*  ％ＳＵＢＡＲＲ…配列の部分取得／部分設定
     C                   EVAL      %SUBARR(WB#ARY2 :  1) = %LIST('AA' :'BB')
     C                   EVAL      %SUBARR(WB#ARY2 :  5) =
     C                               %SUBARR(WB#ARY1 : 9 : 4 )
     C                   EVAL      W#MSG1      = '配列２(1,2,4-9)'
     C                                         + WB#ARY2(01)
     C                                         + WB#ARY2(02)
     C                                         + WB#ARY2(04)
     C                                         + WB#ARY2(05)
     C                                         + WB#ARY2(06)
     C                                         + WB#ARY2(07)
     C                                         + WB#ARY2(08)
     C                                         + WB#ARY2(09)
     C     W#MSG1        DSPLY
     C*
     C                   IF        'BB'       IN WB#ARY2
     C                   EVAL      W#MSG1  = '配列２に"BB"は含まれている'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C                   IF        'BB'       IN %SUBARR(WB#ARY2 :2:1 )
     C                   EVAL      W#MSG1  = '配列２(2) "BB"は含まれている'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C                   IF        NOT ('BB'  IN %SUBARR(WB#ARY2 :3 )   )
     C                   EVAL      W#MSG1  = '配列２(3-) "BB"は含まれない'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     P                 E
     P****************************************************************
     P** < #EOF1        > : 存在の判定                            **
     P****************************************************************
     P #EOF1           B
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ
     FMSYOL01@4 IF   E           K DISK
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D #EOF1           PI
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D  MSYOL01_KEY1   DS                  LIKEREC(MSYOR  : *KEY  ) INZ
     D*---------------
     D* 変数等定義
     D*---------------
     D*商品マスタ・レコード
     D REC1            DS                  LIKEREC(MSYOR     : *ALL )
     D*
     D WB#ARY1         S              2A   DIM(24        )                      配列
     D IX              S              2S 0 INZ                                  配列指標
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     C*---------------------------------------------
     C*参照等の結果に判定関数で直感的
     C*---------------------------------------------
     C*  ％ＥＯＦ…読めなかったら”＊ＯＮ（真）”
     C                   READ      MSYOR         REC1
     C                   IF        NOT %EOF
     C                   EVAL      W#MSG1  = 'READで参照'
     C                                     + REC1.MSYOSYNAME
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*  ％ＦＯＵＮＤ…見つかったら”＊ＯＮ（真）”
     C                   EVAL      MSYOL01_KEY1.MSYOSYCODE = REC1.MSYOSYCODE
     C     MSYOL01_KEY1  CHAIN     MSYOR         REC1
     C                   IF        %FOUND
     C                   EVAL      W#MSG1  = 'CHAINで参照'
     C                                     + REC1.MSYOSYNAME
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*  ％ＥＱＵＡＬ…一致したら”＊ＯＮ（真）”
     C                   EVAL      WB#ARY1     = '--'
     C                   EVAL      WB#ARY1(05) = 'EE'
     C                   EVAL      IX          = 1                              指標
     C     'EE'          LOOKUP    WB#ARY1(IX)                            28
     C                   IF        %EQUAL
     C                   EVAL      W#MSG1  = 'LOOKUP で一致。指標:'
     C                                     + %CHAR(IX)
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*■ＬＯＯＫＵＰ命令の代わりにもっと直感的なＢＩＦも用意されている
     C*  ％ＬＯＯＫＵＰ…配列要素の一致検索
     C                   EVAL      WB#ARY1(06) = 'FF'
     C                   EVAL      IX          = %LOOKUP('FF' : WB#ARY1 )
     C                   IF        IX      > *ZERO
     C                   EVAL      W#MSG1  = '%LOOKUP で一致。指標:'
     C                                     + %CHAR(IX)
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     P                 E
     P****************************************************************
     P** < #SUBR1       > : ○                                    **
     P****************************************************************
     P #SUBR1          B
     D #SUBR1          PI
     D*---------------
     D* 変数等定義
     D*---------------
     C*----------------------------------------------------------------
     C*-* メイン処理                                               **
     C*----------------------------------------------------------------
     C*
     P                 E
