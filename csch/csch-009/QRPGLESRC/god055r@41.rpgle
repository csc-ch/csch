     H*-------------------------------------------------------------------------
     H*■解決
     H*　プロトタイプによりコンパイルエラーが検知できる
     H*　実行時エラーとなりづらい
     H*　可変パラメータを使い元のプログラムで不要な場合は引数設定をなしにできる
     H*　改修インパクトの低下
     H*-------------------------------------------------------------------------
     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #05 便利な…*NOPASS-1（固定            *
     H*   プログラムＩＤ  : GOD055R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/01                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*----<<メインプロシージャー入口>>-----*
     D                 PI
     D  IN_001                       16A                                        引数１
     D*■追加の引数
     D*　＊ＮＯＰＡＳＳ…渡されない可能性がある
     D  IN_002                       16A   OPTIONS(*NOPASS   )                  引数２
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D W#MSG1          S             48A   INZ                                  メッセージ
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   MOVEL     IN_001        W#MSG1                         メッセージ
     C     W#MSG1        DSPLY
     C*
     C*■引数が渡されたか判定
     C*  ％ＰＡＲＭＳ………渡された引数の数
     C*  ％ＰＡＲＭＮＵＭ…引数の引数番号
     C                   IF        %PARMS >= %PARMNUM( IN_002     )
     C                   EVAL      W#MSG1      = %TRIM(IN_002        )          メッセージ
     C                                         + ' - %PARMNUM'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*■引数が渡されたか判定
     C*  ％ＰＡＳＳＥＤ……渡された引数の数（Ｖ７Ｒ４以上※ＰＴＦ必要なケースあり
     C                   IF        %PASSED(IN_002       )
     C                   EVAL      W#MSG1      = %TRIM(IN_002        )          メッセージ
     C                                         + ' - %PASSED'
     C     W#MSG1        DSPLY
     C                   ENDIF
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
