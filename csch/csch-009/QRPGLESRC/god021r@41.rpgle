     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #02 グローバル変数の呪縛（固定           *
     H*   プログラムＩＤ  : GOD021R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/07/10                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     F*商品マスタ
     FMSYOP@4   IF   E           K DISK    ALIAS
     D*-*******************************************************************
     D*-*Ｄ仕様書                                                      **
     D*-*******************************************************************
     D W#MSG1          S             48A   INZ VARYING                          メッセージ
     D*
     D IX              S              3I 0 INZ                                  ＩＮＤＥＸ
     D*
     D W#ZEI_KINGAKU   S              7S 0                                      消費税金額
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C                   FOR       IX          = 1 TO 3
     C                   READ      MSYOR
     C                   IF        %EOF
     C                   LEAVE
     C                   ENDIF
     C*
     C*消費税取得（プロシージャ）
     C*☆入力値はただの引数
     C*☆引数も値渡し／参照渡しを選択できる
     C*☆関数の返り値を使うだけでプロシージャ内の変数は意識しない
     C*☆プロシージャがグローバル変数を使わければ外部変数の汚染はない
     C*☆プロトタイプでコンパイル時に引数や返り値の型が保証されている
     C                   EVAL      W#ZEI_KINGAKU = GET_SYOHI_ZEI(
     C                                                 MSYOP_SYOHIN_TANKA
     C                                               : MSYOP_SYOHIN_ZEI_KUBUN
     C                                             )
     C*
     C*出力
     C                   EVAL      W#MSG1      = MSYOP_SYOHIN_CODE
     C                                         + ' 消費税＝'
     C                                         + %EDITC(W#ZEI_KINGAKU :'N')
     C                                         + '円'
     C*
     C     W#MSG1        DSPLY
     C*
     C                   ENDFOR
     C*
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
     C*-～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～
     C*-～　ここに長～～～～～～～～～～い、処理があっても変数のスコープは独立して安心
     C*-～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～
     P*-***************************************************************
     P*-* 消費税取得                                               **
     P*-***************************************************************
     P GET_SYOHI_ZEI...
     P                 B
     D GET_SYOHI_ZEI...
     D                 PI                  LIKE(W#ZEI_KINGAKU           )       消費税金額
     D*
     D*ＶＡＬＵＥ…値渡し（呼び出し元の変数は汚染されない
     D  IN_TANKA                           LIKE(MSYOP_SYOHIN_TANKA      )       単価
     D                                      VALUE
     D  IN_ZEI_KUBUN                       LIKE(MSYOP_SYOHIN_ZEI_KUBUN  )       税区分
     D                                      VALUE
     D*
     D*----<<ユーザー定義>>-----*
     D L#ZEI_KINGAKU   S                   LIKE(W#ZEI_KINGAKU           )       消費税金額
     D*
     C*-***************************************************************
     C*-* サブプロ・メイン処理                                     **
     C*-***************************************************************
     C*返り値クリアー
     C                   EVAL      L#ZEI_KINGAKU = *ZERO                        消費税金額
     C*
     C*税区分別計算
     C                   SELECT
     C*   消費税一般
     C                   WHEN      IN_ZEI_KUBUN  = '11'
     C                   EVAL      L#ZEI_KINGAKU = IN_TANKA                     消費税金額
     C                                           * 0.10
     C*   消費税軽減
     C                   WHEN      IN_ZEI_KUBUN  = '12'
     C                   EVAL      L#ZEI_KINGAKU = IN_TANKA                     消費税金額
     C                                           * 0.08
     C                   ENDSL
     C*
     C                   RETURN    L#ZEI_KINGAKU
     C*
     P                 E
