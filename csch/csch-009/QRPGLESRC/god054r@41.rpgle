     H*-------------------------------------------------------------------------
     H*■解決
     H*　変数最大…約１６ＭＢ程度まで使用できる（16,773,102～16,773,104
     H*　ベンチは可変長文字列とＥＶＡＬ命令が最速
     H*
     H*■ベンチマーク
     H*　ＲＰＧ４（固定長ＥＶＡＬ…２１１秒
     H*　ＲＰＧ４（可変長ＥＶＡＬ…６秒
     H*　ＲＰＧ４（可変長ＣＡＴ……２３秒
     H*-------------------------------------------------------------------------
     H**********************************************************************
     H*                                                                    *
     H*   システム名      :ＲＰＧ教育                                *
     H*   サブシステム名  :ＩＢＭリスキリング・カレッジ              *
     H*   プログラム名    : #05 便利な…VARYING/VARCHAR（固定      *
     H*   プログラムＩＤ  : GOD054R@41                                 *
     H*   会　社　名　　  :株式会社中部システム                      *
     H*                                                                    *
     H*     作　成　者    :㈱中部システム Y.USHIDA                   *
     H*     作　成　日    : 2025/09/01                                 *
     H*     管　理　番　号: CSC-202507-001                             *
     H*                                                                    *
     H*     変　更　者    :　　　　　　　　　　　　　　　　　　      *
     H*     変　更　日    : ____/__/__                                 *
     H*     管　理　番　号:                                            *
     H*                                                                    *
     H*  プログラム特記事項                                              *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H* 　　　　　　　　　　　　　　　　　　　　　　　　　　　　　　   *
     H*                                                                    *
     H*-*******************************************************************
     H*-*Ｈ仕様書                                                      **
     H*-*******************************************************************
     H DFTACTGRP(*NO) ACTGRP(*NEW)
     H DATEDIT(*YMD)
     H DECEDIT('0.')
     H COPYRIGHT('COPYRIGHT(C) CHUBU SYSTEM CO.,LTD. ALL RIGHTS RESERVED')
     F*-*******************************************************************
     F*-*Ｆ仕様書                                                      **
     F*-*******************************************************************
     D*-********************************************************************
     D*-* Ｄ仕様書                                                      **
     D*-********************************************************************
     D*-----------------------------------
     D*プログラム状況・データ構造
     D*-----------------------------------
     D                SDS
     D S#PROC                  1     10A                                        プログラム名
     D S#STS                  11     15S 0                                      状況コード
     D S#JOB                 244    253A                                        ジョブ名
     D S#USER                254    263A                                        ユーザー名
     D S#JNBR                264    269S 0                                      ジョブ№
     D*-********************************************************************
     D*-* ＫＬＩＳＴ                                                    **
     D*-********************************************************************
     D*-********************************************************************
     D*-* 変数定義                                                      **
     D*-********************************************************************
     D                 DS
     D  XTIME                  1     14  0
     D  XTIME6                 1      6  0
     D  XHH2                   1      2  0
     D  XMM2                   3      4  0
     D  XSS2                   5      6  0
     D*
     D W#MSG1          S             48A   INZ                                  メッセージ
     D*
     D STR1            S           9999A                                        文字列
     D*
     D STR2            S           9999A   VARYING                              文字列
     D*
     D C#MAX           C                   1000000000                           回数
     C*-********************************************************************
     C*-* メインルーチン                                                **
     C*-********************************************************************
     C*初期処理
     C                   EXSR      @INZ
     C*
     C*---------------------------------
     C*ループ処理（固定長
     C*---------------------------------
     C                   Z-ADD     *ZERO         XSECX             5 0          秒計算
     C                   Z-ADD     *ZERO         XSECS             5 0          開始秒
     C                   Z-ADD     *ZERO         XSECE             5 0          終了秒
     C*
     C                   TIME                    XTIME
     C     XHH2          MULT      3600          XSECX                          秒計算
     C                   ADD       XSECX         XSECS                          開始秒
     C     XMM2          MULT      60            XSECX                          秒計算
     C                   ADD       XSECX         XSECS                          開始秒
     C                   ADD       XSS2          XSECS                          開始秒
     C*
     C     1             DO        C#MAX
     C*
     C                   EVAL      STR1        = 'A'                            文字列
     C                                         + ' B'                           文字列
     C*
     C                   ENDDO
     C*
     C                   TIME                    XTIME
     C     XHH2          MULT      3600          XSECX                          秒計算
     C                   ADD       XSECX         XSECE                          終了秒
     C     XMM2          MULT      60            XSECX                          秒計算
     C                   ADD       XSECX         XSECE                          終了秒
     C                   ADD       XSS2          XSECE                          終了秒
     C*
     C     XSECE         SUB       XSECS         XSECX                          秒計算
     C*
     C                   MOVEL     XSECX         W#MSG1                         メッセージ
     C     W#MSG1        CAT       '秒':0      W#MSG1                         メッセージ
     C     W#MSG1        DSPLY
     C*
     C*---------------------------------
     C*ループ処理（可変長
     C*---------------------------------
     C                   Z-ADD     *ZERO         XSECX                          秒計算
     C                   Z-ADD     *ZERO         XSECS                          開始秒
     C                   Z-ADD     *ZERO         XSECE                          終了秒
     C*
     C                   TIME                    XTIME
     C     XHH2          MULT      3600          XSECX                          秒計算
     C                   ADD       XSECX         XSECS                          開始秒
     C     XMM2          MULT      60            XSECX                          秒計算
     C                   ADD       XSECX         XSECS                          開始秒
     C                   ADD       XSS2          XSECS                          開始秒
     C*
     C     1             DO        C#MAX
     C*
     C                   EVAL      STR2        = 'A'                            文字列
     C                                         + ' B'                           文字列
     C*
     C***     'A'           CAT       'B':1         STR2                           文字列
     C*
     C                   ENDDO
     C*
     C                   TIME                    XTIME
     C     XHH2          MULT      3600          XSECX                          秒計算
     C                   ADD       XSECX         XSECE                          終了秒
     C     XMM2          MULT      60            XSECX                          秒計算
     C                   ADD       XSECX         XSECE                          終了秒
     C                   ADD       XSS2          XSECE                          終了秒
     C*
     C     XSECE         SUB       XSECS         XSECX                          秒計算
     C*
     C                   MOVEL(P)  XSECX         W#MSG1                         メッセージ
     C     W#MSG1        CAT       '秒':0      W#MSG1                         メッセージ
     C     W#MSG1        DSPLY
     C*
     C*終了処理
     C                   EXSR      @END
     C*-***************************************************************
     C*-* @INZ        初期処理                                     **
     C*-***************************************************************
     C     @INZ          BEGSR
     C*
     C*システム日付
     C                   TIME                    WTIME            14 0
     C                   MOVEL     WTIME         WTIME6            6 0
     C                   MOVE      WTIME         WDATE8            8 0
     C*
     C                   ENDSR
     C*-***************************************************************
     C*-* @END        終了処理                                     **
     C*-***************************************************************
     C     @END          BEGSR
     C*
     C                   SETON                                        LR
     C                   RETURN
     C*
     C                   ENDSR
