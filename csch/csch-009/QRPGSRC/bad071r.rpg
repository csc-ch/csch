     H*-------------------------------------------------------------------------
     H*-------------------------------------------------------------------------
     H            Y
     F*******************************************************************
     F** #07 便利なＢＩＦ／組込関数                   << BAD071R>>   **
     F**                       WRITTEN  25/09/03 BY CSC Y.USHIDA       **
     F**                       UPDATE   XX/XX/XX BY XXX X.XXXXXXXX     **
     F**--------------- PROGRAM DESCRIPTION ---------------------------**
     F*******************************************************************
     F*-----<<ファイル定義>>-----*
     F*商品マスタ
     FMSYOL01 IF  E           K        DISK
     E*-----<<テーフ゛ル定義>>-----*
     E                    ERR     1   1 48               エラー
     E                    ARY        48  1               配列
     I*******************************************************************
     I           SDS
     I                                        1  10 S#PROC
     I                                       11  150S#STS
     I                                      244 253 S#JOB
     I                                      254 263 S#USER
     I                                      264 2690S#JNBR
     I*
     I            DS
     I                                        1   50W#DEC1
     I                                        1   2 W1DEC1
     I                                        3   5 W2DEC1
     C*******************************************************************
     C**        PLIST                                                  **
     C*******************************************************************
     C*******************************************************************
     C**        KLIST                                                  **
     C*******************************************************************
     C           MSKEY1    KLIST
     C                     KFLD           MSSYHN
     C*******************************************************************
     C**        M A I N   R O U T I N E                                **
     C*******************************************************************
     C*
     C                     EXSR @STR1
     C*
     C                     EXSR @SUB1
     C*
     C                     EXSR @LEN1
     C*
     C                     EXSR @EOF1
     C*
     C                     EXSR @END
     C*
     C*******************************************************************
     C**                  初期処理                                   **
     C*******************************************************************
     C           @INZ      BEGSR
     C*
     C                     TIME           WTIME  140
     C                     MOVELWTIME     WTIME6  60
     C                     MOVE WTIME     WDATE8  80
     C*
     C                     MOVEL*BLANK    W#MSG1 48        メッセージ
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  文字列系                                   **
     C*******************************************************************
     C           @STR1     BEGSR
     C*
     C*---------------------------------------------
     C*数値を文字列表現するために「文字化」が必要
     C*---------------------------------------------
     C                     Z-ADD12345     W#DEC1           数値
     C                     MOVELW#DEC1    W#STR1  7 P      数値（文字
     C           'DEC='    CAT  W#STR1:1  W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C*
     C*数値をカンマ表記だともっと複雑
     C*帳票／画面では”ＥＤＴＣＤＥ／ＥＤＴＷＲＤ”が使えるが...
     C*自前で実装しようと”ゼロサプレス”等を考慮するとより複雑になる
     C                     Z-ADD54321     W#DEC1           数値
     C                     MOVEL*BLANK    W#STR1    P      メッセージ
     C           W1DEC1    IFNE *ZERO
     C           W1DEC1    CAT  ',':0     W#STR1    P      数値（文字
     C                     ENDIF
     C           W#STR1    CAT  W2DEC1:0  W#STR1           数値（文字
     C           'DEC='    CAT  W#STR1:1  W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C*
     C*-------------------------------------------------------
     C*文字列の不要なブランク除去はＲＰＧⅢでは結構面倒
     C*  ■例
     C*  １）チェック命令でブランク以外の文字位置を探査
     C*  ２）探査位置までの有効文字を配列展開
     C*  ３）配列を文字列化
     C*-------------------------------------------------------
     C*
     C*---------------------------------------------
     C*文字列の部分取得
     C*---------------------------------------------
     C                     MOVEL*BLANK    W#STR2 10 P      文字列
     C                     MOVEL*BLANK    W#STR3 38 P      文字列
     C*
     C*先頭３～１０桁のエラーＩＤを取得
     C           7         SUBSTERR,1:4   W#STR2    P      文字列
     C*
     C*残りのエラーメッセージを取得
     C           38        SUBSTERR,1:11  W#STR3    P      文字列
     C           'ERR ID:' CAT  W#STR2:0  W#MSG1    P      メッセージ
     C                     CAT  '(':0     W#MSG1           メッセージ
     C                     CAT  W#STR3:0  W#MSG1           メッセージ
     C                     CAT  ')':0     W#MSG1           メッセージ
     C           W#MSG1    DSPLY
     C*
     C*---------------------------------------------
     C*特定文字を指定文字で置換
     C*※ループ処理で面倒
     C*  ■例
     C*  １）文字列を配列展開
     C*  ２）１文字ずつ探査
     C*  ３）特定文字ならば指定文字へ変換して配列展開
     C*  ４）配列を文字列化
     C*---------------------------------------------
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  長さ／サイズ                               **
     C*******************************************************************
     C           @LEN1     BEGSR
     C*
     C*---------------------------------------------
     C*ある文字列の長さ／サイズを知りたい
     C*---------------------------------------------
     C*残りの入力可能バイト数
     C*----右からループ
     C                     MOVEL'ｱｲｳ'     W#STR4 48 P      文字列
     C                     MOVEAW#STR4    ARY              配列
     C                     Z-ADD48        IX      20       指標
     C           1         DO   48
     C           ARY,IX    IFNE *BLANK
     C                     LEAVE
     C                     ENDIF
     C                     SUB  1         IX               指標
     C                     ENDDO
     C*
     C           48        SUB  IX        W#LEN1  20       残り文字数
     C                     MOVELW#LEN1    W#LEN2  2        残り文字数（文字
     C           '残り:' CAT  W#LEN2    W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C*
     C*----右から探査
     C           ' '       CHEKRW#STR4    IX
     C           48        SUB  IX        W#LEN1  20       残り文字数
     C                     MOVELW#LEN1    W#LEN2  2        残り文字数（文字
     C           '残り:' CAT  W#LEN2    W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C*
     C*---------------------------------------------
     C*配列のサイズ…動的に知る方法はない
     C*---------------------------------------------
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  存在の判定                                 **
     C*******************************************************************
     C           @EOF1     BEGSR
     C*
     C*---------------------------------------------
     C*存在の判定は標識による判定
     C*---------------------------------------------
     C                     READ MSYOR                    90
     C           *IN90     IFEQ *OFF
     C           'READ'    CAT  'あり'  W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C                     ENDIF
     C*
     C           MSKEY1    CHAINMSYOR                91
     C           *IN91     IFEQ *OFF
     C           'CHAIN'   CAT  'あり'  W#MSG1    P      メッセージ
     C           W#MSG1    DSPLY
     C                     ENDIF
     C*
     C                     MOVEA'ABCDE'   ARY              配列
     C                     Z-ADD1         IX               指標
     C           'E'       LOKUPARY,IX                   92
     C           *IN92     IFEQ *ON
     C                     MOVELIX        W#MSG1    P      メッセージ
     C                     CAT  '=INDEX':0W#MSG1           メッセージ
     C           W#MSG1    DSPLY
     C                     ENDIF
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  ○                                         **
     C*******************************************************************
     C           @SUB1     BEGSR
     C*
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  終了処理                                   **
     C*******************************************************************
     C           @END      BEGSR
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C                     ENDSR
**  ﾃｰﾌﾞﾙ/ﾊｲﾚﾂ  -------  ERR
ERR1010001予期しないエラー発生
