     H*-------------------------------------------------------------------------
     H*■課題
     H*　変数スコープを持たないためのミスやコードの可読性低下
     H*-------------------------------------------------------------------------
     H            Y
     F*******************************************************************
     F** #02 グローバル変数の呪縛                     << BAD021R>>   **
     F**                       WRITTEN  25/07/10 BY CSC Y.USHIDA       **
     F**                       UPDATE   XX/XX/XX BY XXX X.XXXXXXXX     **
     F**--------------- PROGRAM DESCRIPTION ---------------------------**
     F*******************************************************************
     F*-----<<ファイル定義>>-----*
     FMSYOP@3 IF  E           K        DISK
     C*******************************************************************
     C**        M A I N   R O U T I N E                                **
     C*******************************************************************
     C*
     C           1         DO   3
     C                     READ MSYOR                    90
     C   90                LEAVE
     C*
     C*消費税取得（サブルーチン）
     C* ▲サブルーチンへ渡すグローバル変数を意識しないといけない
     C* ▲サブルーチン内で返り値のグローバル変数を意識しないといけない
     C* ▲副作用によるグローバル変数の汚染も注意する必要がある
     C* ▲そもそもプロトタイプなどの考えがないため”型”が保証されていない
     C*”Ｚ－ＡＤＤＭＳ...”
     C*”ＭＯＶＥＬＭＳ...”も隣接して可読性が悪い
     C                     Z-ADDMSSYTN    W#TANK  92       単価
     C                     MOVELMSZEIK    W#ZEIK  2        税計算区分
     C                     Z-ADD*ZERO     W#ZEIG  70       消費税額
     C                     EXSR @ZEIG
     C*
     C                     MOVELW#ZEIG    W#MSG1 48 P      メッセージ
     C           MSSYHN    CAT  W#MSG1:1  W#MSG1           メッセージ
     C*
     C           W#MSG1    DSPLY
     C                     ENDDO
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C*～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～
     C*～　ここに長～～～～～～～～～～い、処理があると変数が迷子になる
     C*～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～～
     C*******************************************************************
     C**                  消費税額取得                               **
     C*******************************************************************
     C           @ZEIG     BEGSR
     C*
     C                     SELEC
     C           W#ZEIK    WHEQ '11'
     C           W#TANK    MULT 0.10      W#ZEIG           消費税額
     C           W#ZEIK    WHEQ '12'
     C           W#TANK    MULT 0.08      W#ZEIG           消費税額
     C                     ENDSL
     C*
     C                     ENDSR
