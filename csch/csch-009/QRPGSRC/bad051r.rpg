     H*-------------------------------------------------------------------------
     H*■課題
     H*　同一フィールド名を持つレコードのリネーム方法
     H*-------------------------------------------------------------------------
     H            Y
     F*******************************************************************
     F** #05 便利な…PREFIX                           << BAD051R>>   **
     F**                       WRITTEN  25/08/27 BY CSC Y.USHIDA       **
     F**                       UPDATE   XX/XX/XX BY XXX X.XXXXXXXX     **
     F**--------------- PROGRAM DESCRIPTION ---------------------------**
     F*******************************************************************
     F*-----<<ファイル定義>>-----*
     F*商品マスタ（カナ順
     FMSYOL02 IF  E           K        DISK
     F*■見づらさ（くっついている）／わかりづらさ
     F*　Ｋ…継続行
     F*　ＲＥＮＡＭＥ…リネーム様式
     F            MSYOR                             KRENAMEMSYOR02
     F*商品マスタ（コード順
     FMSYOL01 UF  E           K        DISK
     F            MSYOR                             KRENAMEMSYOR01
     E*-----<<テーフ゛ル定義>>-----*
     I*******************************************************************
     I*-----<<リネーム定義>>-----*
     I*■非効率な点
     I*　レコードのフィールド毎のリネームを一つずつ記述しなければならない
     I*　フィールドの増減に対して、ソースの改変が必要となる
     I*　同じＩ仕様書でもデータ構造より前に記述というルールがある
     IMSYOR01
     I              MSSYHN                          M1SYHN
     I              MSSYNM                          M1SYNM
     I              MSSYKN                          M1SYKN
     I              MSSYTN                          M1SYTN
     I              MSZEIK                          M1ZEIK
     I              MSUPDT                          M1UPDT
     I              MSUPTM                          M1UPTM
     I              MSUPPG                          M1UPPG
     I*******************************************************************
     I           SDS
     I                                        1  10 S#PROC
     I                                       11  150S#STS
     I                                      244 253 S#JOB
     I                                      254 263 S#USER
     I                                      264 2690S#JNBR
     C*******************************************************************
     C**        KLIST                                                  **
     C*******************************************************************
     C           M1KEY1    KLIST
     C                     KFLD           M1SYHN
     C*******************************************************************
     C**        M A I N   R O U T I N E                                **
     C*******************************************************************
     C*
     C                     EXSR @INZ
     C*
     C           1         DO   3
     C                     READ MSYOR02                  90
     C   90                LEAVE
     C*
     C           MSSYHN    CAT  MSSYKN:1  W#MSG1 48        メッセージ
     C*
     C           W#MSG1    DSPLY
     C*
     C                     MOVELMSSYHN    M1SYHN           商品コード
     C           M1KEY1    CHAINMSYOR01              55
     C   55                ITER
     C*
     C                     Z-ADDWDATE8    M1UPDT           更新日付
     C                     Z-ADDWTIME6    M1UPTM           更新時刻
     C                     MOVELS#PROC    M1UPPG           更新ＰＧＭ
     C                     UPDATMSYOR01
     C*
     C                     ENDDO
     C*
     C                     EXSR @END
     C*
     C*******************************************************************
     C**                  初期処理                                   **
     C*******************************************************************
     C           @INZ      BEGSR
     C*
     C                     TIME           WTIME  140
     C                     MOVELWTIME     WTIME6  60
     C                     MOVE WTIME     WDATE8  80
     C*
     C                     ENDSR
     C*******************************************************************
     C**                  終了処理                                   **
     C*******************************************************************
     C           @END      BEGSR
     C*
     C                     SETON                     LR
     C                     RETRN
     C*
     C                     ENDSR
