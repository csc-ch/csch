     A*****************************************************************
     A**         品目マスター照会画面                              **
     A*--------------------------------------------------------------**
     A** 60:ERR_NOT_FOUND                                            **
     A** 61:ERR_NO_MORE                                              **
     A*****************************************************************
     A                                      PRINT
     A                                      CF03(03 '終了')
     A                                      INDARA
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL01)                     **
     A*****************************************************************
     A          R PANEL01                   OVERLAY
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A            D1PGM         10A  O  1  3TEXT('PGM-ID')
     A                                  1 26'商品マスター照会'
     A                                      DSPATR(RI HI)
     A                                  1 63'日付'
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 63'時刻'
     A                                  2 70TIME
     A                                  5  4'商品コード・・・・・'
     A            I1SYCODE       5A  B  5 27
     A                                      ALIAS(I1_SYOHIN_CODE   )
     A            D1MSG         36O  O  5 40TEXT('メッセージ')
     A  60
     AO 61                                  DSPATR(RI)
     A  60                                  COLOR(WHT)
     A  61                                  COLOR(YLW)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL02)                     **
     A*****************************************************************
     A          R PANEL02                   OVERLAY PROTECT
     A                                      CF12(12 '戻る')
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A*
     A                                 10  6'商品名（正式）・・'
     A            D2SYNAME      36O    10 27TEXT('商品名（正式）')
     A                                      ALIAS(D2_SYOHIN_NAME   )
     A*
     A                                 12  6'販売単価・・・・・'
     A            D2SYTANK1      9Y 2  12 27TEXT('商品単価')
     A                                      EDTCDE(N)
     A                                      ALIAS(D2_SYOHIN_TANKA1 )
     A*
     A                                 14  6'仕入単価・・・・・'
     A            D2SYTANK2      9Y 2  14 27TEXT('仕入単価')
     A                                      EDTCDE(N)
     A                                      ALIAS(D2_SYOHIN_TANKA2 )
     A*
     A                                 16  6'税区分・・・・・・'
     A            D2ZEIKBN      26O    16 28TEXT('税区分')
     A                                      ALIAS(D2_SYOHIN_ZEI_KUBUN )
     A*
     A                                 18  6'当月売上数量・・・'
     A            D2SU11         9Y 0  18 28TEXT('当月売上数量')
     A                                      EDTCDE(N)
     A                                      ALIAS(D2_URI_SURYO1    )
     A*
     A                                 20  6'当年売上数量・・・'
     A            D2SU12         9Y 0  20 28TEXT('当年売上数量')
     A                                      EDTCDE(N)
     A                                      ALIAS(D2_URI_SURYO2    )
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL03)                     **
     A*****************************************************************
     A          R PANEL03
     A            D3MSG         76O  O 23  3TEXT('メッセージ')
