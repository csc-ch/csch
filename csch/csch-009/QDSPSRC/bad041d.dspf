     A*****************************************************************
     A**         品目マスター照会画面                              **
     A*****************************************************************
     A                                      PRINT
     A                                      CF03(03 '終了')
     A                                      INDARA
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL01)                     **
     A*****************************************************************
     A          R PANEL01                   OVERLAY
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A            D1PGM         10A  O  1  3TEXT('PGM-ID')
     A                                  1 26'商品マスター照会'
     A                                      DSPATR(RI HI)
     A                                  1 63'日付'
     A                                  1 70DATE
     A                                      EDTCDE(Y)
     A                                  2 63'時刻'
     A                                  2 70TIME
     A                                  5  4'商品コード・・・・・'
     A            I1SYHN         5A  B  5 27
     A            D1MSG         36O  O  5 40TEXT('メッセージ')
     A  60
     AO 61                                  DSPATR(RI)
     A  60                                  COLOR(WHT)
     A  61                                  COLOR(YLW)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL02)                     **
     A*****************************************************************
     A          R PANEL02                   OVERLAY PROTECT
     A                                      CF12(12 '戻る')
     A                                      PAGEUP(33)
     A                                      PAGEDOWN(34)
     A*
     A                                 10  6'商品名（正式）・・'
     A            D2SYNM        36O    10 27TEXT('商品名（正式）')
     A*
     A                                 12  6'販売単価・・・・・'
     A            D2SYT1         9Y 2  12 27TEXT('商品単価')
     A                                      EDTCDE(N)
     A*
     A                                 14  6'仕入単価・・・・・'
     A            D2SYT2         9Y 2  14 27TEXT('仕入単価')
     A                                      EDTCDE(N)
     A*
     A                                 16  6'税区分・・・・・・'
     A            D2ZEIK        26O    16 28TEXT('税区分')
     A*
     A                                 18  6'当月売上数量・・・'
     A            D2SU11         9Y 0  18 28TEXT('当月売上数量')
     A                                      EDTCDE(N)
     A*
     A                                 20  6'当年売上数量・・・'
     A            D2SU12         9Y 0  20 28TEXT('当年売上数量')
     A                                      EDTCDE(N)
     A*****************************************************************
     A**          DISPLAY RECORD(NAME---PANEL03)                     **
     A*****************************************************************
     A          R PANEL03
     A            D3MSG         76O  O 23  3TEXT('メッセージ')
